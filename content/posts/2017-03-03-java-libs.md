+++
draft = true
date = "2017-02-23"
title = "Java - Dom4j, xmlunit"
description = "AWS Deploy - do and don't"
categories = ["aws"]
tags = ["aws", "deploy", "docker", "jar"]
+++

## Dom4j

```groovy
package com.viacom.arcops

import javaslang.collection.List
import org.dom4j.Document
import org.dom4j.Element
import org.dom4j.Node
import org.dom4j.io.SAXReader
import spock.lang.Specification
import spock.lang.Unroll

import static com.viacom.arcops.FilesHelper.readAsStream

@Unroll
class Dom4jSpec extends Specification {

    def "dom4j should extract node value with xpath"() {
        given:
        String xmlFile = "pts/sample/pack-002/0b4e5446-a09d-431e-8e96-70e2cb122e89.xml"
        SAXReader reader = new SAXReader()
        Document doc = reader.read(readAsStream(xmlFile))

        when:
        Node shortName = doc.selectSingleNode("//tveFeed/tveItem/Clip/ShortName")

        then:
        shortName.getText() == " BET Hip Hop Awards 2016 Special BET Hip Hop Awards 2016 DJ Khaled "
    }

    def "dom4j should extract attribute with xpath"() {
        given:
        String xmlFile = "pts/sample/pack-002/0b4e5446-a09d-431e-8e96-70e2cb122e89.xml"
        SAXReader reader = new SAXReader()
        Document doc = reader.read(readAsStream(xmlFile))

        when:
        Element genre = doc.selectSingleNode("//tveFeed/tveItem/Clip/Genre") as Element

        then:
        genre.attribute("value").getText() == "Awards Show"
    }

    def "dom4j should list all children nodes"() {
        given: "xml file"
        String xmlFile = "pts/sample/pack-002/0b4e5446-a09d-431e-8e96-70e2cb122e89.xml"
        SAXReader reader = new SAXReader()
        Document doc = reader.read(readAsStream(xmlFile))

        when: "xml is parsed and first-level children are extracted"
        Element root = doc.getRootElement()
        List<String> elementsNames = List.ofAll(root.elements()).map({ e -> e.getName() })


        then: "it should return 4 elements"
        elementsNames == List.of("network", "package", "user", "tveItem")

        and: "root should have name 'tveFeed'"
        root.getName() == "tveFeed"
    }

    def "dom4j should be able to refer to parent"() {
        given:
        String xmlFile = "pts/sample/pack-002/0b4e5446-a09d-431e-8e96-70e2cb122e89.xml"
        SAXReader reader = new SAXReader()
        Document doc = reader.read(readAsStream(xmlFile))
        Element root = doc.getRootElement()

        when:
        Element clip = root.element("tveItem").element("Clip").element("SeasonNumber").getParent()

        then:
        clip.getName() == "Clip"
    }
}
```

## xmlunit

```groovy
package com.viacom.arcops

import org.xmlunit.builder.DiffBuilder
import org.xmlunit.diff.Diff
import spock.lang.Specification
import spock.lang.Unroll

@Unroll
class XmlDiffSpec extends Specification {

    def "it should not find difference in identical xmls"() {
        given:
        String xml = """<book category="CHILDREN"><title lang="en">Harry Potter</title><author>J K. Rowling</author><year>2005</year><price>29.99</price></book>"""

        when:
        Diff diff = DiffBuilder
                .compare(xml)
                .withTest(xml)
                .build()

        then:
        !diff.hasDifferences()
    }

    def "it should not find difference in xmls with different formatting"() {
        given:
        String xmlA = """
    <book category="CHILDREN">
    <title lang="en">Harry Potter</title>
    <author>J K. Rowling</author>
    <year>2005</year>
    <price>29.99</price>
</book>"""
        String xmlB = """<book category="CHILDREN"><title lang="en">Harry Potter</title><author>J K. Rowling</author><year>2005</year><price>29.99</price></book>"""

        when:
        Diff diff = DiffBuilder
                .compare(xmlA)
                .withTest(xmlB)
                .ignoreWhitespace()
                .build()

        then:
        !diff.hasDifferences()
    }

    def "it should find difference in different xmls"() {
        given:
        String xmlA = """<book category="CHILDREN"><title lang="en">Harry Potter</title><author>J K. Rowling</author><year>2005</year><price>29.99</price></book>"""
        String xmlB = """<book category="ADULTS"><title lang="en">Harry Potter</title><author>J K. Rowling</author><year>2005</year><price>29.99</price></book>"""

        when:
        Diff diff = DiffBuilder
                .compare(xmlA)
                .withTest(xmlB)
                .ignoreWhitespace()
                .build()

        then:
        diff.hasDifferences()
        diff.getDifferences().asList().size() == 1
        diff.getDifferences().asList().first().toString().contains("Expected attribute value 'CHILDREN' but was 'ADULTS'")
    }
}
```

## Helper

```groovy
package com.viacom.arcops

import groovy.transform.CompileStatic
import groovy.transform.TypeChecked

import static java.nio.charset.StandardCharsets.UTF_8

@CompileStatic
@TypeChecked
final class FilesHelper {

    private FilesHelper() {}

    static String readAsText(String filename) {
        InputStream inputStream = readAsStream(filename)
        String input = inputStream.getText(UTF_8.name())
        return input
    }

    static InputStream readAsStream(String filename) {
        InputStream inputStream = FilesHelper.class.getClassLoader().getResourceAsStream(filename)
        if (inputStream == null) {
            throw new FileNotFoundException("$filename not found")
        }
        return inputStream
    }
}
```